plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.50'
    id 'antlr'
}

group 'net.arithmetic'
version '1.0.0'

def antlrPackage = rootProject.group

repositories {
    mavenCentral()
}

/**
 * Without the next section Gradle will add a 'compile' dependency on Antlr3:
 * https://github.com/gradle/gradle/issues/820
 */
configurations {
    compile {
        extendsFrom = extendsFrom.findAll { it != configurations.antlr }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    antlr "org.antlr:antlr4:4.7.2"
    implementation "org.antlr:antlr4-runtime:4.7.2"

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.5.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.5.2'
}

compileKotlin.dependsOn generateGrammarSource

generateGrammarSource {
    maxHeapSize = "64m"
    outputDirectory = file("${buildDir}/generated-src/antlr/main/${antlrPackage.replace('.', '/')}")

    arguments += [
            "-visitor",
            "-no-listener",
            "-package", antlrPackage
    ]
}

test {
    useJUnitPlatform()
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}